buildscript {
    repositories {
        mavenCentral()

        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }

        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version=project_version
group=project_groupId
archivesBaseName=project_name

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"
}

repositories() {
    mavenLocal()

    maven {
        name = "elmakers-repo"
        url = "https://maven.elmakers.com/repository/"
    }
}

configurations {
    provided

    shade
    compile.extendsFrom shade
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

dependencies {
    provided 'com.nationcraft.party:api:1.2-SNAPSHOT'

    provided 'org.spigotmc:spigot:1.7.8-R0.1-SNAPSHOT'

    provided fileTree(dir: 'libraries', include: ['*.jar'])
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
